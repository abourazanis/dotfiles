# If profiling is enabled, collect timing information
if [ ! -z "$ZSH_PROF" ]; then
  zmodload zsh/zprof
fi

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# use .localrc for SUPER SECRET STUFF that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# initialize autocomplete here, otherwise functions won't be loaded
#autoload -U compinit
#compinit

# Spaceship theme
autoload -U promptinit; promptinit
prompt spaceship

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# ZPLUG env
export ZPLUG_HOME=/usr/local/opt/zplug
source $ZPLUG_HOME/init.zsh

#############################################
#               Plugins
#############################################

# ruby
zplug "plugins/rvm",            from:oh-my-zsh, lazy:1

[ -z "${PIPENV_ACTIVE}" ] && {
    zplug "plugins/virtualenvwrapper", from:oh-my-zsh
}

# nodejs
## lazy loading is around 70x faster
export NVM_LAZY_LOAD=true
zplug "lukechilds/zsh-nvm"


zplug "zsh-users/zsh-history-substring-search"
zplug "zsh-users/zsh-syntax-highlighting", defer:2

zplug mafredri/zsh-async, from:github


# Install packages that have not been installed yet
if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
        echo; zplug install
    else
        echo
    fi
fi

# Then, source plugins and add commands to $PATH
zplug load

# If profiling is enabled, display timing information
if [ ! -z "$ZSH_PROF" ]; then
  zprof
fi
